import me.modmuss50.mpp.ReleaseType

plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.13-SNAPSHOT" apply false
    id "me.modmuss50.mod-publish-plugin" version "1.1.0"
}

apply from: 'https://raw.githubusercontent.com/FTBTeam/mods-meta/main/gradle/changelog.gradle'

architectury {
    minecraft = rootProject.minecraft_version
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"
    apply from: "https://raw.githubusercontent.com/FTBTeam/mods-meta/main/gradle/git-md-changelog.gradle"

    base {
        archivesName = project.archives_base_name
    }

    version = project.mod_version
    group = project.maven_group
    archivesBaseName = project.archives_base_name

    // needs to be done AFTER version is set
    apply from: "https://raw.githubusercontent.com/FTBTeam/mods-meta/main/gradle/publishing.gradle"
//    apply from: "https://raw.githubusercontent.com/FTBTeam/mods-meta/main/gradle/extra-local-mods.gradle"

    repositories {
        mavenLocal()
        maven {
            url "https://maven.architectury.dev/"
        }

        maven {
            url "https://maven.ftb.dev/releases"
            content {
                includeGroup "dev.ftb.mods"
            }
        }

        maven {
            url "https://maven.ftb.dev/snapshots"
            content {
                includeGroup "dev.ftb.mods"
            }
        }

        maven {
            url "https://maven.blamejared.com"
            content {
                includeGroup "mezz.jei"
            }
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 21
    }

    java {
        withSourcesJar()
    }
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "maven-publish"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.officialMojangMappings()
    }

    publishing {
        repositories {
            if (ftbPublishing.ftbToken) {
                maven {
                    url ftbPublishing.ftbURL
                    credentials {
                        username = ftbPublishing.ftbUser
                        password = ftbPublishing.ftbToken
                    }
                }
            }
        }
    }
}

publishMods {
    dryRun = providers.environmentVariable("CURSEFORGE_KEY").getOrNull() == null
    changelog = createChangelog(project)
    version = mod_version

    def tag = providers.environmentVariable("TAG").getOrElse("release")
    type = tag.endsWith("-beta") ? BETA : (tag.endsWith("-alpha") ? ALPHA : STABLE)

    def createOptions = (String projectName) -> {
        publishOptions {
            file = project.provider { project(":$projectName").tasks.remapJar }.flatMap { it.archiveFile }
            displayName = "[${projectName.toUpperCase()}][${minecraft_version}] ${readable_name} ${mod_version}"
            modLoaders.add(projectName.toLowerCase())
        }
    }

    def fabricOptions = createOptions("fabric")
    def neoForgeOptions = createOptions("neoforge")
//	def forgeOptions = createOptions("forge")

    def curseForgeOptions = curseforgeOptions {
        accessToken = providers.environmentVariable("CURSEFORGE_KEY")
        projectId = project.curseforge_id
        minecraftVersions.add(rootProject.minecraft_version)

        requires {
            slug = 'architectury-api'
        }
    }

//    curseforge("forge") {
//        from (curseForgeOptions, forgeOptions)
//    }

    curseforge("neoforge") {
        from (curseForgeOptions, neoForgeOptions)
    }

    curseforge("fabric") {
        from (curseForgeOptions, fabricOptions)
        requires {
            slug = "fabric-api"
        }
    }

    // TODO uncomment this one day but we need to redo how our publishing workflow happens
//    github {
//        repository = "ftbteam/ftb-filter-system"
//        accessToken = providers.environmentVariable("GITHUB_TOKEN")
//        commitish = providers.environmentVariable("GITHUB_SHA").orElse("dryRun")
//        tagName = providers.environmentVariable("GITHUB_REF_NAME").orElse("dryRun")
//
//        // Gradle doesn't like accessing the buildApi task directly so we need to use the provider
//        file = project.provider { project(":common").tasks.buildApi }.flatMap { it.archiveFile }
//    }
}
